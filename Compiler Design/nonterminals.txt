
S -> title dramatis-personae actlist

title -> wordlist peq
wordlist -> word_stuff wordlist | word_stuff
word_stuff -> WORDS | NOT | SO | IF | FROM | DIFFERENT | MOST | LEAST | AT | LESS | MORE | THAN | WORSE | BETTER | IS | PERCHANCE | PERADVENTURE | FORSOOTH | VERILY | YEA | NAY | TO | OUGHT | MUST | SHALL | WE | US | LET | POSTHASTE | PROCEED | OPPOSITE | HALF | BETWEEN | OF | OR | EITHER | BOTH | THRICE | TWICE | CUBE | SQUARE | ROOT | REMAINDER | QUOTIENT | PRODUCT | DIFFERENCE | SUM | NOTHING | YOURSELF | MYSELF | POSSESSIVE | AS | AM | RETURN | SCENE | ACT | SOME_ROMAN_NUMBERS | I | AND | OMNES | EXEUNT | EXIT | ENTER | RECALL | REMEMBER | NEG_ADJ | POS_ADJ | NEG_NOUN | POS_NOUN | THE | ARTICLE | ARE | YOU | LISTEN_TO_YOUR_HEART | OPEN_YOUR_HEART | OPEN_YOUR_MIND | SPEAK_YOUR_MIND | ACTOR
peq -> PERIOD | QUESTION | EXCLAMATION
pe -> PERIOD | EXCLAMATION
dramatis-personae -> ACTOR COMMA wordlist peq dramatis-personae
dramatis-personae -> ACTOR COMMA wordlist peq

actlist -> act actlist | act

act -> ACT SOME_ROMAN_NUMBERS COLON wordlist peq scenelist
act -> ACT I COLON wordlist peq scenelist

scenelist -> scene scenelist | scene
scene -> SCENE SOME_ROMAN_NUMBERS COLON wordlist peq statement-list
scene -> SCENE I COLON wordlist peq statement-list

statement-list -> statement statement-list | lambda
statement -> enter-stmt | exit-stmt | line
line -> ACTOR COLON optional-addressee sentence-list
optional-addressee -> ACTOR COMMA | lambda
optional-interjection -> interjection COMMA | lambda
interjection -> NAY | YEA | VERILY | FORSOOTH | PERADVENTURE | PERCHANCE
sentence-list -> optional-interjection sentence sentence-list | optional-interjection sentence
sentence -> io pe | assignment pe | goto pe | stackop pe | question QUESTION | conditional

io -> SPEAK_YOUR_MIND | OPEN_YOUR_MIND | OPEN_YOUR_HEART | LISTEN_TO_YOUR_HEART

enter-stmt -> LBRACKET ENTER ACTOR RBRACKET | LBRACKET ENTER actorlist RBRACKET
exit-stmt -> LBRACKET EXIT ACTOR RBRACKET
exit-stmt -> LBRACKET EXEUNT actorlist RBRACKET
exit-stmt -> LBRACKET EXEUNT OMNES RBRACKET
exit-stmt -> LBRACKET EXEUNT RBRACKET

assignment -> YOU ARE AS adj AS arithmetic
assignment -> YOU ARE arithmetic
assignment -> YOU value
assignment -> I AM arithmetic

arithmetic -> ARTICLE value | THE value | POSSESSIVE value | YOU | MYSELF | YOURSELF | ACTOR
arithmetic -> NOTHING
arithmetic -> THE SUM OF arithmetic AND arithmetic
arithmetic -> THE DIFFERENCE BETWEEN arithmetic AND arithmetic
arithmetic -> THE PRODUCT OF arithmetic AND arithmetic
arithmetic -> THE QUOTIENT OF arithmetic AND arithmetic
arithmetic -> THE QUOTIENT BETWEEN arithmetic AND arithmetic
arithmetic -> THE REMAINDER OF arithmetic AND arithmetic
arithmetic -> THE REMAINDER BETWEEN arithmetic AND arithmetic
arithmetic -> THE SQUARE OF arithmetic
arithmetic -> THE CUBE OF arithmetic
arithmetic -> THE SQUARE ROOT OF arithmetic
arithmetic -> TWICE arithmetic
arithmetic -> THRICE arithmetic
arithmetic -> HALF arithmetic
arithmetic -> BOTH arithmetic AND arithmetic
arithmetic -> EITHER arithmetic OR arithmetic
arithmetic -> THE OPPOSITE OF arithmetic

value -> adj-list noun | noun

actorlist -> ACTOR COMMA actorlist | ACTOR AND ACTOR
noun -> POS_NOUN | NEG_NOUN
adj -> POS_ADJ | NEG_ADJ
adj-list -> adj adj-list | lambda


let_us -> LET US | WE SHALL | WE MUST | WE OUGHT TO
goto -> let_us PROCEED TO goto-target
goto -> let_us PROCEED POSTHASTE TO goto-target
goto -> let_us RETURN

goto-target -> ACT SOME_ROMAN_NUMBERS COMMA SCENE SOME_ROMAN_NUMBERS
goto-target -> ACT I COMMA SCENE SOME_ROMAN_NUMBERS
goto-target -> ACT SOME_ROMAN_NUMBERS COMMA SCENE I
goto-target -> ACT I COMMA SCENE I
goto-target -> ACT I
goto-target -> ACT SOME_ROMAN_NUMBERS
goto-target -> wordlist

stackop -> REMEMBER arithmetic
stackop -> RECALL wordlist

question -> AM I comparison
question -> IS arithmetic comparison
question -> ARE YOU comparison

comparison -> BETTER THAN arithmetic
comparison -> MORE adj THAN arithmetic
comparison -> AS adj AS arithmetic
comparison -> WORSE THAN arithmetic
comparison -> LESS adj THAN arithmetic
comparison -> AT LEAST AS adj AS arithmetic
comparison -> AT MOST AS adj AS arithmetic
comparison -> DIFFERENT FROM arithmetic

conditional -> IF SO COMMA optional-interjection sentence
conditional -> IF NOT COMMA optional-interjection sentence

